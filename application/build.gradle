// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext.versions = [
            // build
            'versionCode'                 : 1,
            'versionName'                 : "1.0",
            'minSdk'                      : 21,
            'targetSdk'                   : 31,
            'compileSdk'                  : 31,
            'gradle'                      : '4.0.1',
            'kotlin'                      : '1.4.10',
            'gradleBuildTools'            : '7.0.2',
            'gradlePlugin'                : '1.5.30',

            // core
            'coreKtx'                     : "1.6.0",
            'appCompat'                   : "1.3.1",
            'hilt'                        : '2.38.1',
            'fragmentKtx'                 : "1.3.6",
            'architectureComponents'      : "2.3.1",
            'constraintLayout'            : "2.1.1",
            'materialDesign'              : '1.4.0',
            'junit'                       : "4.13.2",
            'datastorePreferences'        : '1.0.0',
            'timber'                      : '4.7.1',
            'activityKtx'                 : '1.3.1',
    ]

    ext.exoPlayerLibVersions = [
            'lotteanim'        : '3.7.0',
            'exoplayer'        : '2.15.1',
    ]

    ext.sharedGroup = {dependencyHandler->
        delegate = dependencyHandler
        implementation "androidx.core:core-ktx:${versions.coreKtx}"
        implementation "androidx.appcompat:appcompat:${versions.appCompat}"
        implementation "androidx.fragment:fragment-ktx:${versions.fragmentKtx}"
        implementation "com.google.android.material:material:${versions.materialDesign}"
        implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
        testImplementation "junit:junit:${versions.junit}"
        implementation "com.jakewharton.timber:timber:${versions.timber}"
        implementation "androidx.activity:activity-ktx:${versions.activityKtx}"
    }

    ext.lifeCycleDependenciesGroup = {dependencyHandler->
        delegate = dependencyHandler
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:${versions.architectureComponents}"
        implementation "androidx.lifecycle:lifecycle-common-java8:${versions.architectureComponents}"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.architectureComponents}"
    }

    ext.hiltDependenciesGroup = {dependencyHandler->
        delegate = dependencyHandler
        implementation "com.google.dagger:hilt-android:${versions.hilt}"
        kapt "com.google.dagger:hilt-compiler:${versions.hilt}"
    }

    ext.animationDependenciesGroup = {dependencyHandler->
        delegate = dependencyHandler
        implementation "com.airbnb.android:lottie:${exoPlayerLibVersions.lotteanim}"
    }

    ext.player = {dependencyHandler->
        delegate = dependencyHandler
        //implementation "com.google.android.exoplayer:exoplayer:${exoPlayerLibVersions.exoplayer}"
        implementation "com.google.android.exoplayer:exoplayer-core:${exoPlayerLibVersions.exoplayer}"
        implementation "com.google.android.exoplayer:exoplayer-dash:${exoPlayerLibVersions.exoplayer}"
        implementation "com.google.android.exoplayer:exoplayer-hls:${exoPlayerLibVersions.exoplayer}"
        implementation "com.google.android.exoplayer:exoplayer-ui:${exoPlayerLibVersions.exoplayer}"
        implementation "com.google.android.exoplayer:extension-ima:${exoPlayerLibVersions.exoplayer}"
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradleBuildTools}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${versions.hilt}"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}